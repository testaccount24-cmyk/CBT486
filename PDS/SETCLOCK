         MACRO
&LBL     CALLRTN &RTN,&PARM= CALL A ROUTINE WITH OPTINAL PARM
&L       SETC  '&LBL'
         AIF   ('&PARM' EQ '').SETR15
&L       LA    R1,&PARM      PASS THE PARM
&L       SETC ''
.SETR15  ANOP
&L       LA    R15,&RTN      PT TO ROUTINE
         BAKR  *-*,R15       CALL IT
         MEND
*---------------------------------------------------------------------*
*                                                                     *
* THIS PROGRAM MANAGES LOCAL TIME AS THE OFFSET FROM GMT. IT NORMALLY *
*   RUNS AS A CONSOLE DIALOG. POSSIBLE RESPONSES ARE:                 *
*                                                                     *
*     1. QUERY - DISPLAY CURRENT LOCAL TIME ZONE OFFSET               *
*     2. SET   - CHANGE CURRENT LOCAL TIME ZONE OFFSET                *
*     3. END   - EXIT FROM DIALOG                                     *
*                                                                     *
* THE PROGRAM ACCEPTS EITHER OR BOTH OF TWO EXEC PARMS:               *
*                                                                     *
*     1. TEST  - SIMULATES CHANGES INTERNALLY                         *
*     2. AUTO  - NON-DIALOG MODE FOR AUTOMATION ENVIRONMENT           *
*                                                                     *
*     THESE PARMS MAY APPEAR IN EITHER ORDER, NO DELIMITERS.          *
*                                                                     *
* FOR AUTO PROCESSING, THE REQUESTED TIMEZONE (3 CHARACTERS) MUST     *
*     APPEAR IN COLUMNS 9-11. FOR NON-TEST AUTO MODE, ANY PLACEHOLDER *
*     STRING (SUCH AS 'PROD') MAY BE USED IN PLACE OF 'TEST'.         *
*                                                                     *
*                                                                     *
*                                                                     *
*                                                                     *
* WRITTEN 95/02 BY J. O. SKIP ROBINSON, PROGRAM LOGIC SERVICES        *
*                                                                     *
*                                                                     *
*---------------------------------------------------------------------*
SETCLOCK AMODE 31
SETCLOCK RMODE ANY
SETCLOCK START
         SAVE  (14,12),,SCE_SETCLOCK._&SYSDATE._&SYSTIME._
*
         LR    R12,R15       SET BASE REG
         USING SETCLOCK,R12  ENABLE BASE REG
*
         L     R2,0(R1)      PT TO EXEC PARM, IF ANY
CKPARM1  DS    0H            CK FIRST OR ONLY PARM
         CLC   0(2,R2),=Y(4) LONG ENOUGH FOR A PARM?
         BL    CKPARMX       N: SKIP CHECKING
CKTEST1  CLC   =C'TEST',2+0(R2) REQ TEST PROC'G?
         BNE   CKAUTO1       N: CK NEXT
         OI    FLAGS,TESTMODE Y: SET FLAG
         B     CKPARM2       CONTINUE
CKAUTO1  CLC   =C'AUTO',2+0(R2) REQ AUTOMATION PROC'G?
         BNE   CKPARM2       N: CONTINUE
         OI    FLAGS,AUTOMODE SET FLAG
CKPARM2  DS    0H            CK 2ND PARM, IF ANY
         CLC   0(2,R2),=Y(4+4) LONG ENOUGH FOR 2ND PARM?
         BL    CKTZSET       N: CONTINUE
CKTEST2  CLC   =C'TEST',2+4(R2) REQ TEST PROC'G?
         BNE   CKAUTO2       N: CK NEXT
         OI    FLAGS,TESTMODE Y: SET FLAG
         B     CKTZSET       CONTINUE
CKAUTO2  CLC   =C'AUTO',2+4(R2) REQ AUTOMATION PROC'G?
         BNE   CKTZSET       N: CONTINUE
         OI    FLAGS,AUTOMODE SET FLAG
CKTZSET  TM    FLAGS,AUTOMODE IS THIS AUTO MODE?
         BNO   CKPARMX       N: CONTINUEM
         CLC   0(2,R2),=Y(4+4+3) LONG ENOUGH FOR TZ PARM?
         BNL   CKTZSET2      Y: CONTINUE
         WTO   MF=(E,MSGA)   COMPLAIN
         LA    R15,12        THIS IS A NO-NO
         B     RETURN        OUTTA HERE
CKTZSET2 MVC   NEWTZ,2+8(R2) SAVE AUTO SET TZ
CKPARMX  DS    0H            DONE WITH EXEC PARMS
         TM    FLAGS,TESTMODE RUNNING IN TEST MODE
         BNO   MAINLINE      N: CONTINUE
         WTO   MF=(E,MSG11)  LET 'EM KNOW
*
*---------------------------------------------------------------------*
*
MAINLINE DS    0H            MAIN LINE
         TM    FLAGS,AUTOMODE IS THIS AUTO MODE
         BO    DOAUTO        Y: HANDLE IT
         CALLRTN DIALOG      N: ENTER DIALOG MODE
         SLR   R15,R15       GOOD RET CODE
         B     RETURN        SAY GOODNIGHT, GRACIE
DOAUTO   CALLRTN AUTORTN     ENTER AUTO MODE
         B     RETURN        SAY GOODNIGHT, GRACIE
*
*---------------------------------------------------------------------*
*
DIALOG   DS    0H            CONSOLE DIALOG
         CALLRTN WTOR,PARM=MSG1 PROMPT FOR ACTION
CKEND    SLR   R15,R15       GOOD RET CODE IN CASE
         CLC   =C'E',REPLY   WANTS OUT?
         BE    DIALOGX       Y: DO IT
CKQUERY  CLC   =C'Q',REPLY   WE HAVE A LOOKER?
         BNE   CKSET         N: CK NEXT
         CALLRTN DISPZONE    DISPLAY CURRENT TIME ZONE
         B     DIALOG        ASK AGAIN
CKSET    CLC   =C'S',REPLY   DO WE HAVE A SHAKER?
         BNE   BADREQ        N: THAT'S ALL I KNOW
         CALLRTN SETLCLTM    GO SET AS REQ'D
         CALLRTN DISPZONE    SHOW OUR GOOD WORK
         B     DIALOG        SEE WHAT'S NEXT
BADREQ   MVC   MSG2ERR,REPLY ECHO JUNK
         WTO   MF=(E,MSG2)     TO CONSOLE
         B     DIALOG        TRY AGAIN
DIALOGX  DS    0H            END OF DIALOG
         PR    ,             WHENCE WE CAME
*
*---------------------------------------------------------------------*
*
AUTORTN  DS    0H            AUTOMATION MODE, TZ SUPPLIED
         CALLRTN FINDNAME,PARM=NEWTZ FIND TZ IN TBL
         LR    R11,R1        COPY ENTRY PTR
         LTR   R15,R15       VALID TZ SUPPLIED?
         BZ    AUTORTN2      YESH
         LA    R15,12        N: SET NAUGHTY NAUGHTY
         B     AUTORTNX      SPLIT
AUTORTN2 DS    0H
         CALLRTN DISPZONE    SHOW WHAT WE STARTED WITH
         CALLRTN SETNOSWP    GET HYPER
         CALLRTN BLDNEWCL,PARM=ZONEOFFS-ZONENTRY(R11) CALC NEW TIME
         CALLRTN ISSUECMD    MAKE IT HAPPEN
         CALLRTN SETOKSWP    CHILL OUT
         MVC   MSG5NTZ,NEWTZ NEW TZ TO MSG
         WTO   MF=(E,MSG5)   SAY WHAT WE DID
         CALLRTN DISPZONE    SHOW RESULTS
AUTORTNX PR    ,             WHENCE WE CAME
*
*---------------------------------------------------------------------*
*
DISPZONE DS    0H            DISPLAY CURRENT TIME ZONE
         L     R15,CVTPTR    PT TO CVT
         USING CVTMAP,R15    ENABLE CVT DSECT
         L     R1,CVTTZ      LOCAL TIME ZONE OFFSET
         DROP  R15           DISABLE CVT DSECT
         SLR   R0,R0         CLEAR REG FOR MULTIPLY
         M     R0,=F'1048576' CONVERT TO SECONDS (1.048576)
         D     R0,=F'3600'   CONVERT TO SECONDS
         CVD   R1,DBLWK      TO DECIMAL
         UNPK  WORK(4),DBLWK+3(3) UNPACK A SECTION
         MVC   MSG6HOUR,WORK GET JUST THE WHOLE HOURS
         CLI   MSG6HOUR,C'0' PRETTY IT
         BNE   DISPZON2        UP FOR
         MVI   MSG6HOUR,C' '     DISPLAY
DISPZON2 MVC   MSG6DIRN,=C'WEST' ASSUME WE'RE WEST OF GMT
         LTR   R1,R1         NEGATIVE TIME ZONE OFFSET?
         BM    DISPZON4      GOOD GUESS
         MVC   MSG6DIRN,=C'EAST' SO WE'RE THE OTHER WAY
DISPZON4 DS    0H
         WTO   MF=(E,MSG6)   THIS IS WHAT'S CURRENT
DISPZONX PR    ,             ALL GOOD THINGS...
*
*---------------------------------------------------------------------*
*
SETLCLTM DS    0H            DO IT MY WAY
         MVC   NEWTZ,=CL3' ' CLEAN SLATE
         CALLRTN WTOR,PARM=MSG3 FIND OUT WHAT TZ IS REQ'D
         CLC   =C'END',NEWTZ BAIL OUT PREMATURELY?
         BE    SETLCLTX      IF YOU SAY SO
         CALLRTN FINDNAME,PARM=NEWTZ FIND IT IN OUR TBL
         LR    R11,R1        SAVE RESULT
         LTR   R15,R15       GOT A MATCH?
         BNZ   SETLCLTM      N: TRY TRY AGAIN
         CALLRTN BLDNEWCL,PARM=ZONEOFFS-ZONENTRY(R11) CALC NEW TIME
*
         TIME  DEC,          DECIMAL FORMAT                            *
               TIMEOLD,      WHERE TO PUT IT                           *
               LINKAGE=SYSTEM, NON-SVC INTERFACE                       *
               MF=(E,TIMEP)  USE THE PARM LIST
*
         CLC   TIMENEWD,TIMEOLDD NEW YEAR VS. OLD YEAR
         BH    SETLCLT4      IF HIGHER, MOVE IS FORWARD
         BL    SETLCLT2      IF LOWER,  MOVE IS BACKWARD
         CLC   TIMENEWT,TIMEOLDT NEW TIME VS. OLD TIME
         BNL   SETLCLT4      IF NOT LOWER, NO WARNING
SETLCLT2 DS    0H
         WTO   MF=(E,MSG9)   Y: BETTER THINK THIS THRU
SETLCLT4 DS    0H
         MVC   MSG4TIME,CLOCKNTM NEW TIME TO MSG
         MVC   MSG4DATE,CLOCKNDT NEW DATE TO MSG
         CALLRTN WTOR,PARM=MSG4 IS THIS WHAT YOU WANT?
         CLC   =C'Y',REPLY   IF YOU SAY SO,
         BE    SETLCLT6        WHO AM I TO ARGUE
         CLC   =C'N',REPLY   HAVING 2ND THOUGHTS?
         BE    SETLCLTX      Y: BE THAT WAY
         B     SETLCLT4      I DIDN'T CATCH THAT
SETLCLT6 DS    0H
         CALLRTN SETNOSWP    GET HYPER
         CALLRTN BLDNEWCL,PARM=ZONEOFFS-ZONENTRY(R11) CALC NEW TIME
         CALLRTN ISSUECMD    MAKE IT HAPPEN
         CALLRTN SETOKSWP    CHILL OUT
SETLCLTX SLR   R15,R15       SAY WE DID GOOD
         PR    ,             HOME TO PAPA
*
*---------------------------------------------------------------------*
*
FINDNAME DS    0H            FIND TZ IN TBL BY NAME
         LA    R0,ENTRYNUM   GET ENTRY COUNT
         LA    R6,ZONENTRY   PT TO 1ST ENTRY
NAMELOOP DS    0H            CK ALL ENTRIES
         CLC   ZONENAME-ZONENTRY(,R6),0(R1) GOT A MATCH?
         BE    NAMEFND       OH YES
         LA    R6,ENTRYLEN(,R6) N: NEXT ENTRY
         BCT   R0,NAMELOOP   TRIP THE LIGHT FANTASTIC
         MVC   MSG8NTZ,NEWTZ THIS IS NOT GOOD
         WTO   MF=(E,MSG8)   TELL HER SO
         LA    R15,4         FOR OUR CALLER
         B     FINDNAMX      SCRAM
NAMEFND  DS    0H            WE HIT
         LR    R1,R6         PT TO RESULT FOR CALLER
         LA    R15,0         WE DID FINE
FINDNAMX PR    ,             OH THAT HOME SWEET HOME
*
*---------------------------------------------------------------------*
*
WTOR     DS    0H            GET SOME JUNK FROM THE CONSOLE
         MVI   REPLY,C' '               START WITH A
         MVC   REPLY+1(L'REPLY-1),REPLY   TIDY PAIL
         XC    WTORECB,WTORECB SO WE CAN WAIT FOREVER
         WTOR  MF=(E,(1))    TELL 'EM WHAT WE WANT
         WAIT  ECB=WTORECB JUST A STANDIN' ON THE CORNER
         PR    ,             IT'S BEEN SUCH A LONG TIME
*
*---------------------------------------------------------------------*
*
BLDNEWCL DS    0H            BUILD NEW CLOCK (TIME/DATE)
         LR    R11,R1        PT TO TIME ZONE OFFSET
*
         TIME  MIC,          MICROSECOND FORMAT                        *
               TIMEGMT,      WHERE TO PUT IT                           *
               ZONE=GMT,     TIME ZONE                                 *
               LINKAGE=SYSTEM, NONSVC INTERFACE                        *
               MF=(E,TIMEP)
*
         CONVTOD CONVVAL=TIMEGMT, INPUT FROM TIME MACRO                *
               TIMETYPE=MIC, MICROSECOND FORMAT                        *
               OFFSET=(11),  USE CALLER'S OFFSET                       *
               TODVAL=TODNEW, WHERE TO PUT IT                          *
               MF=(E,CONVTODP)
*
         STCKCONV STCKVAL=TODNEW, INPUT FORM CONVTOD MACRO             *
               CONVVAL=TIMENEW, WHERE TO PUT IT                        *
               TIMETYPE=DEC,DATETYPE=YYYYDDD, OUTPUT FORMAT            *
               MF=(E,STCKCNVP)
*
* FOLLOWING CODE CONVERTS TIME/DATE FROM PACKED TO DISPLAY
*
         UNPK  WORK(6+1),TIMENEW(3+1) HHMMSS + GARBAGE BYTE
         MVC   CLOCKNHH,WORKHH HOURS
         MVC   CLOCKNMM,WORKMM MINUTES
         MVC   CLOCKNSS,WORKSS SECONDS
*
         UNPK  WORK(8+1),TIMENEW+8(4+1) 0YYYYDDD + GARBAGE BYTE
         MVC   CLOCKNYY,WORKYY YEAR
         MVC   CLOCKNDD,WORKDD JULIAN DAY
*
BLDNEWCX PR    ,             AND BACK
*
*---------------------------------------------------------------------*
*
ISSUECMD DS    0H            ISSUE MVS COMMAND
         TM    FLAGS,TESTMODE TEST MODE?
         BNO   ISSUECM2      N: PROCEED
         MVC   MSG7SETC,CLOCKCMS SET UP SIMULATED CMD
         WTO   MF=(E,MSG7)   ISSUE SAME
         B     ISSUECMX      WE'RE DONE
ISSUECM2 DS    0H
         MODESET MODE=SUP,KEY=ZERO FEEL THE POWER
         LA    R2,CLOCKCMD   PT TO CMD
         LA    R3,0          UNIVERSAL CONSOLE ID
         MGCRE TEXT=(2), OUT IT GOES                                   *
               CONSID=(3),                                             *
               MF=(E,CMD1)
         MODESET MODE=PROB,KEY=NZERO MERE MORTAL ONCE AGAIN
ISSUECMX PR    ,             ALL DONE
*
*---------------------------------------------------------------------*
*
SETNOSWP DS    0H            SET OURSELVES NONSWAPPABLE
         TM    FLAGS,TESTMODE TEST MODE?
         BO    SETNOSWX      Y: MIGHT NOT BE AUTHORIZED
         MODESET MODE=SUP    START THE MASQUERADE
         SYSEVENT DONTSWAP   FLY STRAIGHT AND HIGH
         MODESET MODE=PROB   END THE MASQUERADE
SETNOSWX PR    ,             ALL DONE
*
*---------------------------------------------------------------------*
*
SETOKSWP DS    0H            SET OURSELVES SWAPPABLE
         TM    FLAGS,TESTMODE TEST MODE?
         BO    SETOKSWX      Y: MIGHT NOT BE AUTHORIZED
         MODESET MODE=SUP    START THE MASQUERADE
         SYSEVENT OKSWAP     SOFT LANDING
         MODESET MODE=PROB   END THE MASQUERADE
SETOKSWX PR    ,             ALL DONE
*
*---------------------------------------------------------------------*
RETURN   RETURN (14,12),RC=(15) IT WAS A VERY GOOD YEAR
*---------------------------------------------------------------------*
*
         LTORG
*
DBLWK    DS    D             DOUBLE WORD WORK AREA
WTORECB  DS    F             WTOR WAIT ECB
TIMEGMT  DC    4F'0'         GMT STCK
*
TIMENEW  DC    4F'0'         NEW LOCAL STCK
TIMENEWT EQU   TIMENEW+0,8   TIME PORTION
TIMENEWD EQU   TIMENEW+8,4   DATE PORTION
*
TIMEOLD  DC    4F'0'         OLD LOCAL STCK
TIMEOLDT EQU   TIMEOLD+0,8   TIME PORTION
TIMEOLDD EQU   TIMEOLD+8,4   DATE PORTION
*
TODNEW   DC    2F'0'         NEW LOCAL TOD
*
*
* TIME ZONE OFFSET: ((60 X 60 ) / 1.048576) * SIGNED HRS FROM GMT
*
ZONENTRY DS    0F            TIME ZONE ENTRY
ZONEOFFS DS    XL4           HIGH ORDER OF LONG OFFSET
ZONENAME DS    CL3           ZONE NAME
ZONEDESC DS    XL13          ZONE DESCRIPTION
         DS    0F            KEEP ALIGNMENT
ENTRYLEN EQU   *-ZONENTRY    ENTRY LENGTH
         ORG   ZONENTRY      NOW DO IT FOR REAL
*
PSTENTRY DC    X'0000800D',C'PST',C'  8:00 WEST  ' (-)
PDTENTRY DC    X'0000700D',C'PDT',C'  7:00 WEST  ' (-)
MSTENTRY DC    X'0000700D',C'MST',C'  7:00 WEST  ' (-)
MDTENTRY DC    X'0000600D',C'MDT',C'  6:00 WEST  ' (-)
CSTENTRY DC    X'0000600D',C'CST',C'  6:00 WEST  ' (-)
CDTENTRY DC    X'0000500D',C'CDT',C'  5:00 WEST  ' (-)
ESTENTRY DC    X'0000500D',C'EST',C'  5:00 WEST  ' (-)
EDTENTRY DC    X'0000400D',C'EDT',C'  4:00 WEST  ' (-)
*
ENTRYNUM EQU   (*-ZONENTRY)/ENTRYLEN NUMBER OF ENTRIES
*
FLAGS    DC    B'0'          REMEMBER WHAT WE'RE DOING
TESTMODE EQU   B'10000000'   TEST MODE (DIALOG OR AUTO)
AUTOMODE EQU   B'01000000'   AUTOMATION MODE (NON-DIALOG)
*
NEWTZ    DC    CL3' '        NAME OF NEW TIME ZONE
REPLY    DS    CL8           WTOR REPLY AREA
*
WORK     DS    CL10          DECIMAL TIME WORK
WORKHH   EQU   WORK+0,2      HOURS
WORKMM   EQU   WORK+2,2      MINUTES
WORKSS   EQU   WORK+4,2      SECONDS
WORKYY   EQU   WORK+1,4      UEAR
WORKDD   EQU   WORK+5,3      DAY
*
CLOCKCMD DC    Y(CLOCKCML)   MGCRE CMD LENGTH
CLOCKCMS DC    C'SET '       CMD TEST START
CLOCKNEW DC    C'CLOCK=HH.MM.SS,DATE=YYYY.DDD'
CLOCKCML EQU   *-CLOCKCMS    LENGTH OF TEXT ONLY
CLOCKNHH EQU   CLOCKNEW+06,2 HOURS
CLOCKNMM EQU   CLOCKNEW+09,2 MINUTES
CLOCKNSS EQU   CLOCKNEW+12,2 SECONDS
CLOCKNYY EQU   CLOCKNEW+20,4 YEAR
CLOCKNDD EQU   CLOCKNEW+25,3 DAY
CLOCKNTM EQU   CLOCKNEW+06,8 TIME START
CLOCKNDT EQU   CLOCKNEW+20,8 DATE START
*
MSG1     WTOR  'SCESC01A REPLY Q(UERY), S(ET), OR E(ND)',              *
               REPLY,L'REPLY,WTORECB,ROUTCDE=2,MF=L
*
MSG2     WTO   'SCESC02E INVALID REPLY XXXXXXXX',                      *
               ROUTCDE=2,DESC=(7,11),MF=L
MSG2ERR  EQU   MSG2+04+23,L'REPLY
*
MSG3     WTOR  'SCESC03A REPLY NEW TIME ZONE (PDT, PST, ETC.)',        *
               NEWTZ,L'NEWTZ,WTORECB,ROUTCDE=2,MF=L
*
MSG4     WTOR  'SCESC04A REPLY Y OR N TO SET APPROX HH.MM.SS ON YYYY.DD*
               D',                                                     *
               REPLY,L'REPLY,WTORECB,ROUTCDE=2,MF=L
MSG4TIME EQU   MSG4+12+36,L'CLOCKNTM
MSG4DATE EQU   MSG4+12+48,L'CLOCKNDT
*
MSG5     WTO   'SCESC05I TIME ZONE CHANGED TO NTZ',                    *
               ROUTCDE=2,DESC=(7,11),MF=L
MSG5NTZ  EQU   MSG8+04+30,L'ZONENAME
*
MSG6     WTO   'SCESC06I LOCAL TIME IS NOW HH HOURS DIRN OF GMT',      *
               ROUTCDE=2,DESC=(7,11),MF=L
MSG6HOUR EQU   MSG6+04+27,2
MSG6DIRN EQU   MSG6+04+36,4
*
MSG7     WTO   'SET CLOCK=HH.MM.SS,DATE=YYYY.DDD   SIMULATED SETCLOCK C*
               MD',                                                    *
               ROUTCDE=2,DESC=(7,11),MF=L
MSG7SETC EQU   MSG7+04+00,CLOCKCML
*
MSG8     WTO   'SCESC08E REQUESTED TIME ZONE NTZ NOT SUPPORTED',       *
               ROUTCDE=2,DESC=(7,11),MF=L
MSG8NTZ  EQU   MSG8+04+29,L'ZONENAME
*
MSG9     WTO   'SCESC09W !!CAUTION!! BACKWARD TIME CHANGE REQUESTED',  *
               ROUTCDE=2,DESC=(2,7),MF=L
*
MSGA     WTO   'SCESC10E AUTO MODE REQUIRES TIME ZONE PARAMETER',      *
               ROUTCDE=2,DESC=(11),MF=L
*
MSG11    WTO   'SCESC11I SETCLOCK PROGRAM RUNNING IN SIMULATION MODE', *
               ROUTCDE=2,DESC=(7,11),MF=L
*
CMD1     MGCRE MF=L          GENERAL PARM LIST
*
TIMEP    TIME  LINKAGE=SYSTEM,MF=L GENERAL PARM LIST
*
STCKCNVP STCKCONV MF=L       GENERAL PARM LIST
*
CONVTODP CONVTOD MF=L        GENERAL PARM LIST
*
HEXTBL   EQU   *-X'F0'       MYTHICAL START OF HEX XLATE TBL
         DC    C'0123456789' REAL START
         ORG   HEXTBL+X'FA'  LEAP AHEAD
         DC    C'ABCDEF'     LAST ENTRIES
*
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
*
         CVT   DSECT=YES,LIST=YES
*
         END
